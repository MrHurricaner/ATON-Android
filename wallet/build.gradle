apply plugin: 'com.android.application'
apply plugin: 'realm-android'
// 注：需要配置符号表时，使用bugly插件
apply plugin: 'bugly'
bugly {
    appId = '0970ddc6bd'
    appKey = 'b947cbd1-d646-41bc-819c-4447ea7fe428'

}

android {

    compileSdkVersion rootProject.ext.android.compileSdkVersion

    defaultConfig {

        applicationId "com.juzix.wallet"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionName "${version_name}"
        versionCode Integer.parseInt(version_code)
        buildConfigField("String", "BETA_COUNT", "\"${beta_count}\"")
        buildConfigField("String", "RELEASE_TYPE", "${releaseTypeC}")
        buildConfigField("String", "UM_APPKEY", "${UM_APPKEY}")
        buildConfigField("String", "BUGLY_APPID", "${BUGLY_APPID}")
        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true

        ndk {
            abiFilters "armeabi", "armeabi-v7a", "x86"
        }
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        debug {
            storeFile file('../extras/debug.keystore')
        }

        release {
            storeFile file("../extras/wallet.keystore")
            storePassword "123456"
            keyAlias "wallet"
            keyPassword "123456"

            v2SigningEnabled false
        }
    }


    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "RELEASE_TYPE", "${releaseTypeC}")
            multiDexEnabled true
        }

        releaseC {
            debuggable false
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "RELEASE_TYPE", "${releaseTypeC}")
            multiDexEnabled true
        }

        releaseX {
            debuggable false
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "RELEASE_TYPE", "${releaseTypeX}")
            zipAlignEnabled true
            multiDexEnabled true
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def verName = android.defaultConfig.versionName
            def verCode = android.defaultConfig.versionCode
            def innerVer = beta_count
            def releaseTime = new Date().format("yyyyMMddHHmm")
            def gitVersion = getGitVersion();

            def type = ""
            if (variant.buildType.name == "releaseC") {
                type = "c"
            } else if (variant.buildType.name == "releaseX") {
                type = "x_unjiagu"
            } else if (variant.buildType.name == "debug") {
                type = "debug"
                // 调试若使用实时时间戳会导致apk无法找到，因此，这里固定为0
                releaseTime = "0"
                gitVersion = "";
            }
            if (variant.flavorName != "wallet") {
                type = (type + "_" + variant.flavorName)
            }

            outputFileName = "wallet_android_v${verName}_${innerVer}_${releaseTime}_${verCode}_${type}_${gitVersion}.apk";
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def getGitVersion() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

task getReleasePassword << {
    def password = ''
    if (rootProject.file('password.properties').exist()){
        Properties properties = new Properties();
        properties.load(rootProject.file('password.properties').newDataInputStream())
        password = properties.getProperty("release.password");
    }
    if (!password?.trim()){
        password = new String(System.console().readPassword("\n what's the keystore password"))
    }
}

repositories {
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':zxing')
    implementation project(':framework')
    implementation project(':biometric')
    implementation rootProject.ext.dependencies.support_v4
    implementation rootProject.ext.dependencies.gridlayout
    implementation rootProject.ext.dependencies.appcompat_v7
    implementation rootProject.ext.dependencies.support_design
    implementation rootProject.ext.dependencies.support_annotations
    implementation rootProject.ext.dependencies.support_recyclerview
    implementation rootProject.ext.dependencies.support_zxing
    implementation rootProject.ext.dependencies.support_constraintLayout
    implementation rootProject.ext.dependencies.support_multidex
    implementation rootProject.ext.dependencies.web3j_core
    implementation rootProject.ext.dependencies.web3j_crypto
    implementation rootProject.ext.dependencies.bitcoinj_core
    implementation rootProject.ext.dependencies.eventbus
    implementation rootProject.ext.dependencies.rxjava2_rxjava
    implementation rootProject.ext.dependencies.rxjava2_rxandroid
    implementation rootProject.ext.dependencies.rxlifecycle
    implementation rootProject.ext.dependencies.rxlifecycle_android
    implementation rootProject.ext.dependencies.rxbinding3
    implementation rootProject.ext.dependencies.rxpermissions
    implementation rootProject.ext.dependencies.butterknife
    annotationProcessor rootProject.ext.dependencies.butterknife_compiler
    implementation rootProject.ext.dependencies.picker
    implementation rootProject.ext.dependencies.support_zxing
    implementation rootProject.ext.dependencies.flexbox
    implementation rootProject.ext.dependencies.tinypinyin
    implementation rootProject.ext.dependencies.immersionbar
    implementation rootProject.ext.dependencies.smarttablayout
    implementation rootProject.ext.dependencies.umeng_common
    implementation rootProject.ext.dependencies.umeng_analytics
    implementation rootProject.ext.dependencies.bugly
}
